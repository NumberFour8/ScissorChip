#include "msp430.h"
NAME msp_mul32_cios

EXTERN mp_freeze
EXTERN mp_condsub
EXTERN clear_mem

PUBLIC mp_mulmod32_cios  
RSEG CODE 

; Performs modular multiplication of A and B which are both in Montgomery representation.
; This function utilizes fully the 32-bit multiplier.
; The reduction is done modulo 2^255-19
; A is pointed by R13, B is pointed by R14.
; The result T has to be pointed by R12. R15 contains the size of B (in bytes).
; BEWARE: the number T has to be 40 bytes long as the additional 8 bytes of memory is required

mp_mulmod32_cios

  PUSHM.W #8,R11

  PUSH.W R13
  MOV #40,R13
  CALLA #clear_mem 
  POP.W R13

  ; Coarsely Integrated Operand Scanning
  ; m = R4,R7
  ; -------------------------------------- 

  ; Store pointer to T
  MOV R12,R11

 ; For loop i = 0 .. s-1
 ; R15 contains size of B
  ADD R14,R15    ; Traverses B
outer_for3

  ; (C,S) = t[0] + a[0]*b[i]
  MOV 0x0(R14),&MPY32L  ; b[i]
  MOV 0x2(R14),&MPY32H
  MOV 0x0(R13),&OP2L    ; a[0] 
  MOV 0x2(R13),&OP2H

  ; Add t[0], t[0] = S
  ADD RES0,0x0(R12)
  ADDC RES1,0x2(R12)
  ADC &RES2
  ADC &RES3
  
  BIS #32,&MPY32CTL0 ; Switch to MAC, keep b[i] in OP1
  MOV R13,R6  ; Traverses A
  MOV R13,R8
  ADD #32,R6  ; Excluding sth word
  MOV R12,R5  ; Traverses T
  ADD #4,R5   ; Start at t[1] already
  ADD #4,R8   ; and at a[1]
inner_for3a
  
   ; (C,S) = t[j] + a[j]*b[i] + C
   MOV RES2,&RES0   ; C
   MOV RES3,&RES1      
   CLR &RES2
   CLR &RES3
   
   MOV @R8+,&OP2L  ; a[j]
   MOV @R8+,&OP2H
   
   ; t[j] = S
   ADD RES0,0(R5)
   ADDC RES1,2(R5)
   ADC &RES2
   ADC &RES3
   
   ADD #4,R5
   CMP R6,R8            ; &a < &a[s]
   JNE inner_for3a
   
  ADD  RES2,32(R12)     ; t[s] = t[s] + C
  ADDC RES3,34(R12)
  
  ; t[s+1] = C
  CLR 36(R12)
  CLR 38(R12)
  ADC 36(R12)   
  
  ; m = t[0]*n'[0] mod 2^32
  MOV 0(R12),&MPY32L    ; t[0]
  MOV 2(R12),&MPY32H
  MOV #0xCA1B,&OP2L     ; n'[0]
  MOV #0x286B,&OP2H
  
  ; Store m in R4 and R7
  MOV RES0,R4
  MOV RES1,R7
  
  ; (C,S) = t[0] + m*n[0]
  MOV #1,&OP2L          ; 1
  MOV #0,&OP2H
  
  MOV R4,&MAC32L        ; m
  MOV R7,&MAC32H
  MOV #0xFFED,&OP2L     ; n[0]
  MOV #0xFFFF,&OP2H 
  
  MOV R12,R5  ; Traverses T
  ADD #28,R5  ; Excluding (s-1)th word
  ADD #4,R12  ; j = 1 (e.g. start at t[1])

  MOV #0xFFFF,&MAC32L   ; n[j]
  MOV #0xFFFF,&MAC32H
    
inner_for3b
   
    ; (C,S) = t[j] + C + m*n[j]
    MOV RES2,&RES0    ; C
    MOV RES3,&RES1      
    CLR &RES2
    CLR &RES3
    
    ADD  @R12+,&RES0 ; t[j]
    ADDC @R12+,&RES1
    ADC &RES2
    ADC &RES3
    
    MOV R4,&OP2L   ; m 
    MOV R7,&OP2H
   
    ; t[j-1] = S
    MOV RES0,-8(R12)
    MOV RES1,-6(R12)
    
    CMP R12,R5
    JNE inner_for3b ; &t < &t[s]
   
   ; (C,S) = t[s-1] + m*n[s-1] + C
   MOV RES2,&RES0    ; C
   MOV RES3,&RES1      
   CLR &RES2
   CLR &RES3
   
   ADD  0(R12),&RES0 ; t[s-1]
   ADDC 2(R12),&RES1
   ADC &RES2      
   ADC &RES3
    
   MOV R4,&MAC32L      ; m
   MOV R7,&MAC32H
   MOV #0xFFFF,&OP2L   ; n[s-1] 
   MOV #0x7FFF,&OP2H
   
   ; t[s-2] = S
   MOV RES0,-4(R12)
   MOV RES1,-2(R12)
    
   ; Reset pointer to T
   MOV R11,R12
   
   ; t[s] + C
   ADD  32(R12),&RES2
   ADDC 34(R12),&RES3
    
   ; t[s-1] = S
   MOV RES2,28(R12)
   MOV RES3,30(R12)
    
   ; t[s] = t[s+1] 
   MOV 36(R12),32(R12)
   MOV 38(R12),34(R12)
    
   ; t[s] + C
   ADC 32(R12)
   ADC 34(R12)
   
   ADD #4,R14
   CMP R14,R15 
   JNE outer_for3 ; &b < &b[s]
  
  POPM.W #8,R11
  
  CLR R13
  CALLA #mp_freeze ; Conditionally subtract 2^255-19 in constant time
    
RETA

END