#include "msp430.h"
NAME msp_mp_mul32c

PUBLIC mp_condsub
RSEG CODE

; Subtracts 2^255-19 from the number pointed by R12 if the LS bit of R4 is set
; otherwise subtracts nothing.
; If the second LS bit of R4 is set, addition is preformed instead of subtraction. 
mp_condsub
    
    PUSHM.W #5,R9 ; Save R5-R9 
    
    MOV R12,R6
    ADD #30,R6
    
    ; Depending on second bit of R4 we decide whether we subtract or add. This is constant time.
    BIT #2,R4
    JNZ setAdd
    MOV #0xFFED,R5
    MOV #0xFFFF,R8
    MOV #0x7FFF,R9
    JMP andLimbs
setAdd
    MOV #0x0011,R5
    MOV #0x0000,R8
    MOV #0x8000,R9
    JMP andLimbs
andLimbs
   
    ; Need to precompute FFFF or 0 to R4
    AND #1,R4
    MOV R4,R7
    CLR R4
    SUB R7,R4        ; R4 == 0 ? R4 = 0 : R4 = FFFF 

    ; Set or reset the limbs of 2^255-19
    AND R4,R5
    AND R4,R8
    AND R4,R9
    
    SUB R5,0(R12)    ; Subtract LS limb
    MOV SR,R7        ; Save SR
    INCD R12         ; Increase R12
    
    ; Subtract from the internal limbs
    MOV R7,SR
csubin
    SUBC R8,0(R12)
    MOV SR,R7
    INCD R12
    CMP R12,R6
    JZ csubfin
    MOV R7,SR
    JMP csubin
csubfin
    MOV R7,SR
    SUBC R9,0(R12)

    POPM.W #5,R9        ; Restore R5 - R10
RETA

PUBLIC mp_freeze
RSEG CODE

; Conditionally subtract 2^255-19 from the 255-bit number pointed by R12

mp_freeze
 
    CLR R4

; -- The following code does: R4 = (R12 > 2^255-19)
    MOV #0x7FFF,R5
    CMP 0x1e(R12),R5    
    BIS SR,R4
    
    MOV #0xFFFF,R5
    MOV R12,R10
    MOV R12,R6
    ADD #28,R10
    ADD #2,R6       ; R6 contains the stop address, the 2nd LS word
compr
    CMP @R10,R5      
    CLRC            ; Here we care only about equality
    BIS SR,R4       
    
    DECD R10
    CMP R6,R10
    JNZ compr

    MOV #0xFFED,R5      ; Compare the last limb
    CMP @R12,R5
    BIS SR,R4
    
    XOR #1,R4   ; Invert the carry bit
    BIT #3,R4
    MOV SR,R4   ; Carry bit is set if R4 is non-zero
    AND #1,R4   ; Extract carry bit
     
; -- 
    AND #0xFFFD,R4    ; Reset the second bit of R4 for subtraction
    CALLA #mp_condsub

RETA


PUBLIC mp_mulmod32c  
RSEG CODE 

; Performs modular multiplication of A and B which are both in Montgomery representation.
; This function utilizes fully the 32-bit multiplier.
; The reduction is done modulo 2^255-19
; A is pointed by R13, B is pointed by R14.
; The result T has to be pointed by R12. R15 contains the size of B (in bytes).
; BEWARE: the number T has to be 40 bytes long as the additional 8 bytes of memory is required

mp_mulmod32c

PUSHM.W #8,R11
  
  ; Coarsely Integrated Operand Scanning
  ; m = R4,R7
  ; -------------------------------------- 

  ; Store pointer to T
  MOV R12,R11

 ; For loop i = 0 .. s-1
 ; R15 contains size of B
  ADD R14,R15    ; Traverses B
outer_for3

  ; (C,S) = t[0] + a[0]*b[i]
  MOV 0x0(R12),&MPY32L  ; t[0]
  MOV 0x2(R12),&MPY32H
  MOV #1,&OP2L          ; 1
  MOV #0,&OP2H
    
  MOV 0x0(R13),&MAC32L  ; a[0]
  MOV 0x2(R13),&MAC32H
  MOV 0x0(R14),&OP2L    ; b[i] 
  MOV 0x2(R14),&OP2H

  ; t[0] = S
  MOV RES0,0x0(R12)
  MOV RES1,0x2(R12)
   
  MOV R13,R6  ; Traverses A
  MOV R13,R8
  ADD #32,R6  ; Excluding sth word
  MOV R12,R5  ; Traverses T
  ADD #4,R5   ; Start at t[1] already
  ADD #4,R8   ; and at a[1]
inner_for3a
  
   ; (C,S) = t[j] + a[j]*b[i] + C
   MOV RES2,&MPY32L   ; C
   MOV RES3,&MPY32H      
   MOV #1,&OP2L       ; 1
   MOV #0,&OP2H
  
   MOV @R5+,&MAC32L  ; t[j]
   MOV @R5+,&MAC32H
   MOV #1,&OP2L         ; 1
   MOV #0,&OP2H
    
   MOV @R8+,&MAC32L  ; a[j]
   MOV @R8+,&MAC32H
   MOV 0x0(R14),&OP2L    ; b[i] 
   MOV 0x2(R14),&OP2H

   ; t[j] = S
   MOV RES0,-4(R5)
   MOV RES1,-2(R5)
   
   CMP R6,R8            ; &a < &a[s]
   JNE inner_for3a
   
  ADD  RES2,32(R12)     ; t[s] = t[s] + C
  ADDC RES3,34(R12)
  
  ; t[s+1] = C
  CLR 36(R12)
  CLR 38(R12)
  ADC 36(R12)   
  
  ; m = t[0]*n'[0] mod 2^32
  MOV 0(R12),&MPY32L    ; t[0]
  MOV 2(R12),&MPY32H
  MOV #0xCA1B,&OP2L     ; n'[0]
  MOV #0x286B,&OP2H
  
  ; Store m in R4 and R7
  MOV RES0,R4
  MOV RES1,R7
  
  ; (C,S) = t[0] + m*n[0]
  MOV 0(R12),&MPY32L    ; t[0]
  MOV 2(R12),&MPY32H
  MOV #1,&OP2L          ; 1
  MOV #0,&OP2H
  
  MOV R4,&MAC32L        ; m
  MOV R7,&MAC32H
  MOV #0xFFED,&OP2L     ; n[0]
  MOV #0xFFFF,&OP2H 
  
  ; Store C in R8 and R9
  MOV RES2,R8
  MOV RES3,R9
  
  MOV R12,R5  ; Traverses T
  ADD #28,R5  ; Excluding (s-1)th word
  ADD #4,R12  ; j = 1 (e.g. start at t[1])
inner_for3b
   
   ; (C,S) = t[j] + m*n[j] + C
    MOV @R12+,&MPY32L ; t[j]
    MOV @R12+,&MPY32H
    MOV #1,&OP2L      ; 1
    MOV #0,&OP2H
    
    MOV R8,&MAC32L    ; C
    MOV R9,&MAC32H      
    MOV #1,&OP2L      ; 1
    MOV #0,&OP2H
    
    MOV R4,&MAC32L      ; m
    MOV R7,&MAC32H
    MOV #0xFFFF,&OP2L   ; n[j] 
    MOV #0xFFFF,&OP2H
   
    ; t[j-1] = S
    MOV RES0,-8(R12)
    MOV RES1,-6(R12)
    
     ; Store C in R8 and R9
    MOV RES2,R8
    MOV RES3,R9
  
    CMP R12,R5
    JNE inner_for3b ; &t < &t[s]
   
   ; (C,S) = t[s-1] + m*n[s-1] + C
   MOV 0(R12),&MPY32L ; t[s-1]
   MOV 2(R12),&MPY32H
   MOV #1,&OP2L      ; 1
   MOV #0,&OP2H
    
   MOV R8,&MAC32L    ; C
   MOV R9,&MAC32H      
   MOV #1,&OP2L      ; 1
   MOV #0,&OP2H
    
   MOV R4,&MAC32L      ; m
   MOV R7,&MAC32H
   MOV #0xFFFF,&OP2L   ; n[s-1] 
   MOV #0x7FFF,&OP2H
   
   ; t[s-2] = S
   MOV RES0,-4(R12)
   MOV RES1,-2(R12)
    
   ; Store C in R8 and R9
   MOV RES2,R8
   MOV RES3,R9
  
   ; Reset pointer to T
   MOV R11,R12
   
   ; t[s] + C
   ADD  32(R12),R8
   ADDC 34(R12),R9
    
   ; t[s-1] = S
   MOV R8,28(R12)
   MOV R9,30(R12)
    
   ; t[s] = t[s+1] 
   MOV 36(R12),32(R12)
   MOV 38(R12),34(R12)
    
   ; t[s] + C
   ADC 32(R12)
   ADC 34(R12)
   
   ADD #4,R14
   CMP R14,R15 
   JNE outer_for3 ; &b < &b[s]
  
  POPM.W #8,R11
  
  CALLA #mp_freeze ; Conditionally subtract 2^255-19 in constant time
    
RETA

END